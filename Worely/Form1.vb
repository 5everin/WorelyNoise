'Worley noise is generated by using the distance between a point (in this case each pixel in the Picturebox) and the nth nearest point from 
'a set Of randomly generated 'seed' points. So n=2 uses the distance from the second closest seed point to a given point location. 

'Every pixel needs to be checked against every seed point to find the lowest n distances. (In this program the maximum n value is 64)
'Due to this. The size of the on screen window and especially the number of seed points (max 9999) heavily infulences the time taken to generate the bitmap.

Imports System.Numerics
Imports System.Math
Imports System.Threading.Tasks

Public Class Form1
    Dim rng As New Random
    Dim maxseeds As Int32 = 66
    Dim Seeds(9999) As Vector2
    Dim displayArray(1) As Int32
    Dim tim As New Stopwatch
    Dim nval As Int32 = 1

    Private Sub Form1_Load(sender As Object, e As EventArgs) Handles MyBase.Load
        PictureBox1.BackColor = Color.Black
        Setarea()
        Make_seeds()
        TextBox1.Text = maxseeds.ToString
    End Sub

    Public Sub Make_seeds()
        Array.Resize(displayArray, PictureBox1.Width * PictureBox1.Height)
        For f As Int32 = 0 To maxseeds - 1
            Seeds(f).X = rng.Next(PictureBox1.Width + 1)
            Seeds(f).Y = rng.Next(PictureBox1.Height + 1)
        Next
    End Sub

    Public Sub Gen()
        'step through each pixel in the picbox (displayarray) and find the nearest n seed points. (max n set to 64)
        'use the nth nearest to generate the noise pattern.

        'Note: it would be better to just store the raw nth distances in displayarray then use a seperate proceedure to convert the values to colours. Makes things more flexable and the noise can be used for more than just bitmaps.
        Parallel.For(0, displayArray.Length, Sub(pixelnumber As Int32)
                                                 Dim vec As Vector2
                                                 vec.Y = CSng(Floor(pixelnumber / PictureBox1.Width))
                                                 vec.X = pixelnumber - (vec.Y * PictureBox1.Width)
                                                 Dim lvl As Int32 = 64
                                                 Dim low(lvl) As Single
                                                 For q As Int32 = 1 To lvl
                                                     low(q) = 99999999
                                                 Next
                                                 lvl = CInt(NumericUpDown1.Value)
                                                 If lvl > 64 Then lvl = 64
                                                 If lvl >= maxseeds Then lvl = maxseeds - 1
                                                 Dim mynoise As Int32 = lvl
                                                 Dim dist As Single
                                                 For seednumber As Int32 = 0 To maxseeds - 1
                                                     dist = Vector2.Distance(vec, Seeds(seednumber))
                                                     For q As Int32 = 1 To lvl
                                                         If dist < low(q) Then
                                                             For n As Int32 = lvl - 1 To q Step -1
                                                                 low(n + 1) = low(n)
                                                             Next
                                                             low(q) = dist
                                                             Exit For
                                                         End If
                                                     Next q
                                                 Next
                                                 'make some colour values 
                                                 Dim col As Int32
                                                 If CheckBox2.Checked = True Then
                                                     col = Abs(CInt((low(1) - (low(mynoise >> 1))) + (low(1) - low(mynoise - 1)))) 'my noise
                                                 Else
                                                     col = CInt(low(lvl)) 'Standard worley value
                                                 End If
                                                 If col > 255 Then col = 255
                                                 If CheckBox1.Checked = True Then
                                                     displayArray(pixelnumber) = (255 << 24) + ((256 - col) << 16) + ((col) << 8) + (col + 140 - CInt(low(lvl) - low(lvl - 1)) >> 1) ' colour
                                                 Else
                                                     displayArray(pixelnumber) = (255 << 24) + (col << 16) + (col << 8) + col
                                                 End If
                                             End Sub)
    End Sub

    Public Sub Output()
        Dim bmp As New Bitmap(PictureBox1.Width, PictureBox1.Height)
        Dim rec As Rectangle
        rec.Size = PictureBox1.Size
        Dim bmpdata As Imaging.BitmapData = bmp.LockBits(rec, Drawing.Imaging.ImageLockMode.WriteOnly, bmp.PixelFormat)
        Dim ptr As IntPtr = bmpdata.Scan0
        System.Runtime.InteropServices.Marshal.Copy(displayArray, 0, ptr, PictureBox1.Width * PictureBox1.Height)
        bmp.UnlockBits(bmpdata)
        PictureBox1.Image = bmp
    End Sub

    Private Sub Button1_Click(sender As Object, e As EventArgs) Handles Button1.Click
        Label1.Text = "Generating noise"
        Label1.Refresh()
        maxseeds = CInt(Val(TextBox1.Text))
        tim.Start()
        Make_seeds()
        Gen()
        Output()
        tim.Stop()
        info()
        tim.Reset()
    End Sub

    Private Sub Noseedgen_GEN()
        tim.Start()
        Gen()
        Output()
        tim.Stop()
        info()
        tim.Reset()
    End Sub

    Private Sub Info()
        Dim tstring As String = " ms"
        Dim tl As Double = tim.Elapsed.TotalMilliseconds
        If tl >= 1000 Then tl /= 1000 : tstring = " seconds"
        Label1.Text = displayArray.Length.ToString("n0") & " pixels" & vbNewLine & tl.ToString("n") & tstring
    End Sub

    Private Sub PictureBox1_Click(sender As Object, e As EventArgs) Handles PictureBox1.Click
        Dim p As Point
        p.X = (MousePosition.X - Me.Left)
        p.Y = (MousePosition.Y - Me.Top)
        Panel1.Location = p
    End Sub
    Private Sub Form1_SizeChanged(sender As Object, e As EventArgs) Handles Me.SizeChanged
        Setarea()
        Make_seeds()
    End Sub

    Private Sub Setarea()
        PictureBox1.Bounds = Me.Bounds
        PictureBox1.Top = 0
        PictureBox1.Left = 0
    End Sub

    Private Sub NumericUpDown1_ValueChanged(sender As Object, e As EventArgs) Handles NumericUpDown1.ValueChanged
        nval = CInt(NumericUpDown1.Value)
        If PictureBox1.Top = 0 Then Noseedgen_GEN()
    End Sub

    Private Sub CheckBox1_CheckedChanged(sender As Object, e As EventArgs) Handles CheckBox1.CheckedChanged, CheckBox2.CheckedChanged
        Noseedgen_GEN()
    End Sub

End Class
